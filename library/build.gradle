plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
}

group = 'com.github.mastergyp'
version = '0.9.1'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        useIR = true
    }
    buildToolsVersion '31.0.0'

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "citcon-alipay-${variant.name}-${version}.aar"
        }
    }
}

dependencies {
    api files('libs/alipaysdk-15.8.03.210428205839.jar')
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = "com.github.mastergyp"
                artifactId = 'citcon-alipay'
                version = "0.9.1"
            }
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.citcon.sdk'
    PUBLISH_VERSION = '15.8.03'
    PUBLISH_ARTIFACT_ID = 'alipay'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"